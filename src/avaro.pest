Entry = {SOI ~ Line* ~ (Item ~ NEWLINE+)* ~ Item? ~ EOI}

Item  = {Option | Plugin | Commodity | Open | Close| Include | Note | Pad | Balance | Document | Price | Event | Custom | Transaction | Comment}

Option = {"option" ~ Space+ ~ String ~ Space+ ~  String}
Plugin = {"plugin" ~ Space+ ~ String ~  (Space+ ~ String )* }
Commodity = {Date ~ Space+ ~ "commodity" ~ Space+ ~ CommodityName ~  CommodityMeta?}
Open = {Date ~ Space+ ~ "open" ~ Space+ ~ AccountName ~ ( Space+ ~CommodityName ~ (Space* ~ "," ~ Space* ~ CommodityName)*)? ~ CommodityMeta?}
Close = {Date ~ Space+ ~ "close" ~ Space+ ~ AccountName }
Include = {"include" ~ Space+ ~ QuoteString }
Note = {Date ~ Space+ ~ "note" ~ Space+ ~ AccountName ~ Space+ ~ String}
Pad = {Date ~ Space+ ~ "pad" ~ Space+ ~ AccountName ~ Space+ ~ AccountName}
Balance = {Date ~ Space+ ~ "balance" ~ Space+ ~ AccountName ~ Space+ ~ number ~ Space+ ~ CommodityName}
Document = {Date ~ Space+ ~ "document" ~ Space+ ~ AccountName ~ Space+ ~ String}
Price = {Date ~ Space+ ~ "price" ~ Space+ ~ CommodityName ~ Space+ ~ number ~ Space+ ~ CommodityName}
Event = {Date ~ Space+ ~ "event" ~ Space+ ~ String ~ Space+ ~ String}
Custom = {Date ~ Space+ ~ "custom" ~ Space+ ~ String ~ ( Space+ ~ StringOrAccount)+}
Transaction = {Date ~ TransactionFlag? ~ (Space+ ~ QuoteString){0,2} ~  Tags? ~ Links? ~ TransactionDetail}
Comment = {(";" | "*") ~ (!Line ~ ANY)*}

TransactionFlag = { Space+ ~ ("!" | "*") }
Tags = {Tag*}
Tag = {Space+ ~ ("#"~UnquoteString)}
Links = {Link*}
Link = {Space+ ~ ("^"~UnquoteString)}
TransactionDetail = _{identation_push ~ TransactionLines ~ DROP}
TransactionLines = {TransactionLine ~ (TransactionNextLine)*}
TransactionLine = {TransactionPosting | CommodityLine}
TransactionPosting = {TransactionFlag? ~ AccountName ~ (Space+ ~ PostingAmount)? ~ PostingMeta}
TransactionNextLine = _{identation ~ TransactionLine}

PostingAmount = {number ~ Space+ ~ CommodityName}
PostingMeta = {(Space+ ~ "{" ~ Space* ~ PostingCost~ PriceCostDate? ~ "}")? ~ ( Space+~ PostingPrice)?}

PostingCost = {number ~ Space+ ~ CommodityName}
PriceCostDate = _{Space* ~ "," ~ Space* ~ Date }
PostingPrice = {PostingSinglePrice | PostingTotalPrice}

PostingSinglePrice = {"@" ~ Space+~ number~ Space+~ CommodityName}
PostingTotalPrice = {"@@" ~ Space+~ number~ Space+~ CommodityName}

StringOrAccount = { AccountName | String }

CommodityMeta = { identation_push ~ CommodityLines ~ DROP }

identation = _{ Line ~ PEEK_ALL }
identation_push = _{ identation ~ PUSH(Space+) }
CommodityLines = _{CommodityLine ~ (CommodityNextLine)*}
CommodityNextLine = _{identation ~ CommodityLine}
CommodityLine = {String ~ Space* ~ ":" ~Space*~ String}

Date = {ASCII_DIGIT{4} ~ "-"~ ASCII_DIGIT{1,2} ~ "-" ~ ASCII_DIGIT{1,2}}

AccountName = { AccountType ~ (":"~UnquoteString)+}
AccountType = {"Assets" | "Liabilities" | "Equity" | "Income" | "Expenses"}
String = {UnquoteString | QuoteString }
UnquoteString = { ((!("\"" | ":" | "(" | ")" | "," | " " | "\t" | Line) ~ ANY)  | ASCII_ALPHANUMERIC | "." | "_" | "-")+}
QuoteString = @{ "\"" ~ inner ~ "\"" }
CommodityName = {ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "." | "_" | "-" | "'")*}

inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

Space = _{ " " | "\t" }
Line = _{ "\n" | "\r" | "\n\r" }


number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}